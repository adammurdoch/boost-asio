allprojects {
    apply plugin: 'cpp'
}

model {
    repositories {
        libs(PrebuiltLibraries) {
            def boostDir = "/usr/local/Cellar/boost/1.66.0"
            def boostIncludeDir = "$boostDir/include"
            def boostLibraryDir = "$boostDir/lib"

            assert new File(boostDir).exists()
            boostAsio {
                headers.srcDir boostIncludeDir
            }
            boostSystem {
                headers.srcDir boostIncludeDir
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("$boostLibraryDir/libboost_system.a")
                }
            }

            def protobufDir = "/usr/local/Cellar/protobuf/3.5.1"
            def protobufIncludeDir = "$protobufDir/include"
            def protobufLibraryDir = "$protobufDir/lib"

            assert new File(protobufDir).exists()
            protobuf {
                headers.srcDir protobufIncludeDir
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("$protobufLibraryDir/libprotobuf.a")
                }
            }
        }
    }
}

subprojects {
    model {
        binaries {
            all {
                cppCompiler.args '-std=c++11'

                // See https://github.com/gradle/gradle-native/issues/551
                cppCompiler.args '-nostdinc'
            }
        }
    }

    model {
        components {
            if (path.startsWith(':client-') || path.startsWith(':server-')) {
                main(NativeLibrarySpec) {
                    binaries.all {
                        lib project: ':common', library: 'main', linkage: 'api'
                        lib project: ':', library: 'boostAsio', linkage: 'api'
                        lib project: ':', library: 'boostSystem', linkage: 'static'
                        if (path.endsWith('1')) {
                            lib project: ':', library: 'protobuf', linkage: 'static'
                        }
                    }
                }
            }
        }
    }
}

project(':app') {
    model {
        components {
            main(NativeExecutableSpec) {
                binaries.all {
                    rootProject.allprojects.findAll { it.path.startsWith(':client-') || it.path.startsWith(':server-') }.each {
                        lib project: it.path, library: 'main'
                    }
                    lib project: ':common', library: 'main', linkage: 'static'
                    lib project: ':', library: 'boostAsio', linkage: 'api'
                    lib project: ':', library: 'boostSystem', linkage: 'static'
                }
            }
        }
    }
}

project(':empty') {
    model {
        components {
            main(NativeLibrarySpec)
        }
    }
}

project(':proto-message') {
    model {
        components {
            main(NativeLibrarySpec) {
                binaries.all {
                    lib project: ':', library: 'protobuf', linkage: 'static'
                }
            }
        }
    }
}

project(':common') {
    model {
        components {
            main(NativeLibrarySpec) {
                binaries.all {
                    lib project: ':', library: 'boostAsio', linkage: 'api'
                    lib project: ':', library: 'boostSystem', linkage: 'static'
                }
            }
        }
    }
}

