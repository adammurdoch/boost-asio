allprojects {
    apply plugin: 'cpp'
}

model {
    repositories {
        libs(PrebuiltLibraries) {
            def linux = System.getProperty("os.name").toLowerCase().contains("linux")

            def boostIncludeDir
            def boostLibraryDir
            if (linux) {
                boostIncludeDir = "/usr/include"
                boostLibraryDir = "/usr/lib/x86_64-linux-gnu"
            } else {
                // Assume macOS
                def boostDir = "/usr/local/Cellar/boost/1.66.0"
                assert file(boostDir).exists()
                boostIncludeDir = "$boostDir/include"
                boostLibraryDir = "$boostDir/lib"
            }

            boostAsio {
                headers.srcDir boostIncludeDir
            }
            boostSystem {
                headers.srcDir boostIncludeDir
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("$boostLibraryDir/libboost_system.a")
                }
            }
            boostFileSystem {
                headers.srcDir boostIncludeDir
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("$boostLibraryDir/libboost_filesystem.a")
                }
            }

            def protobufIncludeDir
            def protobufLibraryDir
            if (linux) {
                protobufIncludeDir = "/usr/include"
                protobufLibraryDir = "/usr/lib/x86_64-linux-gnu"
            } else {
                // Assume macOS
                def protobufDir = "/usr/local/Cellar/protobuf/3.5.1"
                assert file(protobufDir).exists()
                protobufIncludeDir = "$protobufDir/include"
                protobufLibraryDir = "$protobufDir/lib"
            }

            protobuf {
                headers.srcDir protobufIncludeDir
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("$protobufLibraryDir/libprotobuf.a")
                }
            }
        }
    }
}

subprojects {
    model {
        binaries {
            all {
                cppCompiler.args '-std=c++11'

                if (gradle.gradleVersion != '4.2.1') {
                    // See https://github.com/gradle/gradle-native/issues/551
                    cppCompiler.args '-nostdinc'
                }
            }
        }
    }

    model {
        components {
            if (path.startsWith(':client-') || path.startsWith(':server-')) {
                main(NativeLibrarySpec) {
                    binaries.all {
                        lib project: ':common', library: 'main', linkage: 'api'
                        lib project: ':', library: 'boostAsio', linkage: 'api'
                        lib project: ':', library: 'boostSystem', linkage: 'static'
                        if (path.endsWith('1')) {
                            lib project: ':', library: 'protobuf', linkage: 'static'
                        }
                        if (path.endsWith('6')) {
                            lib project: ':', library: 'boostFileSystem', linkage: 'static'
                        }
                    }
                }
            }
        }
    }
}

project(':app') {
    model {
        components {
            main(NativeExecutableSpec) {
                binaries.all {
                    rootProject.allprojects.findAll { it.path.startsWith(':client-') || it.path.startsWith(':server-') }.each {
                        lib project: it.path, library: 'main'
                    }
                    lib project: ':common', library: 'main', linkage: 'static'
                    lib project: ':', library: 'boostAsio', linkage: 'api'
                    lib project: ':', library: 'boostSystem', linkage: 'static'
                }
            }
        }
    }
}

project(':empty') {
    model {
        components {
            main(NativeLibrarySpec)
        }
    }
}

project(':proto-message') {
    model {
        components {
            main(NativeLibrarySpec) {
                binaries.all {
                    lib project: ':', library: 'protobuf', linkage: 'static'
                }
            }
        }
    }
}

project(':common') {
    model {
        components {
            main(NativeLibrarySpec) {
                binaries.all {
                    lib project: ':', library: 'boostAsio', linkage: 'api'
                    lib project: ':', library: 'boostSystem', linkage: 'static'
                }
            }
        }
    }
}

